?start: [(state)+]

// statement
?state: expr
      | function
      | assignment
      | echo_state
function: "fn" new_symbol "(" [parameter ("," parameter)*] ")" ":" start "end"
assignment: "var" new_symbol "=" expr
echo_state: "echo" expr

// expression
?expr: term
     | addition
     | subtraction
     | function_call
addition: expr "+" term
subtraction: expr "-" term
function_call: symbol "(" [expr ("," expr)*] ")"

?term: fact
     | multiplication
     | division
multiplication: term "*" fact
division: term "/" fact

?fact: number
     | symbol
     | priority
?priority: "(" expr ")"
symbol: WORD
number: SIGNED_NUMBER

new_symbol: WORD
parameter: WORD

%import common.WORD
%import common.SIGNED_NUMBER
%import common.WS
%ignore WS
