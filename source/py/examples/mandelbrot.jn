/* mandelbrot.jn */

let xmin = -2.0
let xmax = 1.0
let ymin = -1.5
let ymax = 1.5
let width = 80.0
let height = 40.0
let threshold = 1000

fn in_mandelbrot(x0: Float, y0: Float, n: Int) -> Bool:
    var x: Float = 0.0
    var y: Float = 0.0
    var xtemp: Float
    while n > 0:
        xtemp = x*x - y*y + x0
        y = 2.0*x*y + y0
        x = xtemp
        n = n - 1
        if x*x + y*y > 4.0:
            return False
        end
    end
    return True
end

fn mandel() -> Int:
     var dx: Float = (xmax - xmin)/width
     var dy: Float = (ymax - ymin)/height

     var y: Float = ymax
     var x: Float

     while y >= ymin:
         x = xmin
         while x < xmax:
             if in_mandelbrot(x,y,threshold):
                echo '*'
             end else:
                echo '.'
             end
             x = x + dx
         end
         echo '\n'
         y = y - dy
     end
     return 0
end

fn main() -> Int:
    return mandel()
end
